/**
> Новая номинация: "Приз за самую порнографичную игру" - 50 cad (около 2500 руб.) от нашего уважаемого спонсора.
challenge accepted

ну например, с платьями, капитан обмолвится где-нибудь что ему нравятся красные/белые/хз
с едой можно еще
пусть она на кухне приготовит что-нибудь ему

**/

:s
pln <center><h4>Мария Целеста</h4></center>
pln История одного дела знаменитого командора Вуда.
pln Игра содержит откровенные сцены и не предназначена для игроков младше 18 лет.
btn s2, Начать
btn authors, Автор
end

:authors
pln Автор: Акела
pln Тестирование и советы: Шушкарт
pln Иллюстрации: Gurulax
btn s, Назад
end

:s2
pln Когда я наконец нашла командора Вуда, тот сидел в массажном кресле и попивал виски из стакана. По всей видимости, ему
_ было не слишком удобно: из-за работы массажного кресла виски по большей части обливал командору одежду и лицо, из-за
_ чего тот, вероятно, и сидел с закрытыми глазами, но услужливый робот-бармен вновь наполнял бокал до краев. Хорошо зная
_ своего друга и начальника, я сразу поняла что сегодня у него хорошее настроение.
pln - Доброе утро! Похоже мы взяли новое дело, командор? - поинтересовалась я и наклонилась на ним так, чтобы ему было
_ лучше видно мою шикарную грудь когда он откроет глаза.
pln Вуд осторожно приоткрыл один глаз и недоуменно посмотрел мне в глаза:
pln - С чего вы взяли, доктор Андерсон?
pln - Я применила ваш знаменитый метод. У нас на корабле появилась эта комната отдыха, виски, робот бармен и еще какие-то
_ женщины. Наверное вы взяли задание и нам заплатили аванс? - захлопала ресницами я.
pln Вуд самодовольно ухмыльнулся и снова откинулся в кресле зажмурив глаз, который подозрительно покраснел:
pln - Ха! Не зря Казахстанские ученые доказали что у женщин мозг с грецкий орех. - наконец сказал он. - Я взял аванс и
_ отказался от задания.

btn think1, Придумать что-нибудь чтобы заставить командора взяться за здание
btn wait1, Вздохнуть и уйти к себе в каюту
btn flirt1, Соблазнить детектива Вуда
end

:flirt1
pln - Детектив, может выпьем по чашечке кофе в моей каюте сидя на кровати при плохом освещении?
pln - Предпочитаю виски - отрезал Вуд.
pln Не смотря на весь свой могучий интеллект командор плохо понимал намеки.
btn think1, Придумать что-нибудь чтобы заставить командора взяться за здание
btn wait1, Вздохнуть и уйти к себе в каюту
end

:think1
pln - Как хорошо что мы свободны командор! Я как раз отправила своей маме сообщение что мы находимся в этом квадрате,
_ она вот-вот прибудет сюда навестить меня!
pln - Что?! - Вуд вскочил с кресла и бешено завертел глазами. - Мы немедленно отправляемся!
pln - Но детектив, а как же...
pln - Отставить вопросы, второй пилот Андерсон! По местам! - перебил меня Вуд и выбежал из комнаты отдыха в неизвестном направлении.
btn robot_barmen, Испробовать робота-бармена
btn mostik1, Вздохнуть и отправиться на место второго пилота
end

:robot_barmen
robot_barmen_check = 1
pln Я заметила что на полу валялась инструкция к роботу и решила сначала полистать ее:
pln - Более пятисот видов коктейлей и режим для сексуальных утех! Вау!
btn robot_barmen2, Сделать "Маргариту"
/** btn robot_barmen3, Включить режим сексуальных утех **/
btn robot_barmen2, Понажимать разные кнопки
end

:robot_barmen2
pln Ну уж нет! Я просто обязана попробовать режим сексуальных утех.
image botbarmen.png
pln Однако меня ждало большое разочарование.

:robot_barmen3
pln - Инструкция врет, он не умеет делать ничего кроме виски со льдом! - возмутилась я изучив программы робота. - Какое бесполезное приобретение!
btn mostik1, Вздохнуть и отправиться на место второго пилота
end

:wait1
pln Спустя пару дней Вуд без стука ворвался ко мне в каюту и начал нервно ходить из угла в угол, что-то обдумывая.
pln - Командор, я только проснулась и совсем не одета, - засмущалась я. - Можете сами взглянуть.
pln - Пустяки, - отмахнулся Вуд. - У нас проблемы: они требуют вернуть деньги.
pln - Кто? - спросила я сексуально потягиваясь, но детектив был слишком погружен в свои мысли и не обращал на меня внимания.
pln - Проклятые заказчики! - заворчал он. - Всегда найдут лазейки в этих дурацких законах. Придется лететь и разбираться
_ с их делом. О чем я только думал когда брал это задание! Встретимся на мостике, второй пилот Андерсон!
pln - Но...
pln  Командор не стал меня слушать, а исчез из комнаты так же внезапно как и появился.

btn cls & dress = 1 & goto mostik1, Надеть платье с глубоким вырезом и отправиться на место второго пилота
btn cls & dress = 2 & goto mostik1, Надеть платье покороче и отправиться на место второго пилота
btn mostik1, Надеть костюм второго пилота и отправиться на место второго пилота
end

:mostik1
p На корабле командора Вуда, который он выиграл в покер благодаря своей специальной иннвационной технике игры и
_ крапленым картам, второй пилот означал "единственный пилот", или что еще хуже "единственный трезвый пилот".
_ Но в работе на командора были и свои плюсы, - например мой облегающий костюм второго пилота, отлично подчеркивающий фигуру
if dress = 0 then pln .
if dress = 1 or dress = 2 then pln , который должен был быть на мне сейчас вместо платья.
pln Когда я поднялась на мостик Вуд был уже на месте.
if robot_barmen_check = 1 then pln - Командор! - воскликнула я. - Ваш робот-бармен просто ужасен, он не умеет делать ничего кроме виски со льдом.
if robot_barmen_check = 1 then pln - Я удалил все остальные программы - отмахнулся Вуд. - Они бесполезны.
if robot_barmen_check = 1 then pln Я не нашлась, что возразить.
pln - Координаты и описание задания где-то у меня на почте - сообщил Вуд. - Среди спама.
pln - Командор, надо сначала приземлиться для дозаправки и высадить всех этих женщин - возразила я. - На корабле нет даже припасов.
_ У вас остались какие-нибудь деньги от аванса?
pln - Если появятся вопросы - ищи меня где угодно кроме моей каюты. - сделав вид что не услышал меня продолжил Вуд спускаясь с мостика. - Мне надо поспать.

fuel = 1
mission = 1
money = 0

goto mostik

/** mission 1 **/

:kitchen_1
:kitchen_2
:kitchen_3
:kitchen_4
:kitchen_5
:kitchen_6
:kitchen_7
:kitchen_8
pln Кухня это специально оборудованное для приготовления пищи корабельное помещение. Повара у нас нет, поэтому мы пользуемся им только для хранения тюбиков с едой.
if tube = 0 then btn tube = 1 & cls & goto kitchen_take, Взять <p style="color: red">красный</p> тюбик
if tube = 0 then btn tube = 2 & cls & goto kitchen_take, Взять <p style="color: green">зеленый</p> тюбик
if tube = 0 then btn tube = 3 & cls & goto kitchen_take, Взять <p style="color: blue">синий</p> тюбик
btn ship, Выйти
end

:kitchen_take
pln Я беру тюбик из холодильника.
btn ship, Выйти
end

:engineroom_1
:engineroom_2
:engineroom_3
pln В машинное отделение занимает всю нижнюю палубу, никогда там не была, и надеюсь что не побываю.
goto ship

:gateways_1
:gateways_2
:gateways_3
:gateways_4
:gateways_5
:gateways_6
:gateways_7
:gateways_8
pln Я в шлюзовом отделении. Это единственный выход из корабля. Здесь же расположены скафандры.
btn ship, Вернуться в коридор
end

:apartment1_1
pln Командор уснул в одежде прямо на полу. Совсем себя не жалеет.
btn apartment1_1_stolik, Осмотреть журнальный столик
if count_apartment1_1_obisk = 0 and money = 0 then btn apartment1_1_obisk, Обыскать его каюту в поисках денег
btn ship, Выйти из каюты
end

:apartment1_1_stolik
pln На журнальном столике Вуда лежат журналы. Командор прямолинеен и многое понимает буквально.
if count_apartment1_1_stolik_2 = 0 then btn apartment1_1_stolik_2, Полистать один из журналов
btn apartment1_1, Отойти от столика
end

:apartment1_1_stolik_2
pln Все журналы как на подбор непристойного содержания. Подумав, я взяла "Космоплей". Журнал был в отвратительном
_ состоянии, из него зачем-то повырезали куски:
image magazine.png
pln Остальные журналы на столике выглядели не лучше.
btn apartment1_1, Отойти от столика
end

:apartment1_1_obisk
money = 1
pln Обыскав каюту командора я нашла оставленные мне деньги у него в кошельке.
btn ship, Выйти из каюты
end

:apartment2_1
:apartment2_2
:apartment2_3
:apartment2_4
:apartment2_5
:apartment2_6
:apartment2_8
pln Моя каюта. Тут я сплю и иногда еще чем-нибудь занимаюсь.
if mission = 1 and money = 0 and count_apartment2_1_obisk = 0 then btn apartment2_1_obisk, Обыскать каюту в поисках денег
btn change_clotches, Переодеться
btn apartment2_mirror, Посмотреть в зеркало
btn ship, Выйти из каюты
end

:room_1
:room_2
:room_8
pln В комнате отдыха никого нет.
btn room_2_barmen, Воспользоваться роботом-барменом
btn ship, Выйти из комнаты
end

:room_2_barmen
pln Ну уж нет! У меня полно дел.
goto room_2
end

:change_clotches
pln Я открыла шкаф и решила надеть:
btn cls & dress = 0 & pln Готово! & goto apartment2_1, Костюм второго пилота
btn cls & dress = 1 & pln Готово! & goto apartment2_1, Платье с глубоким вырезом
btn cls & dress = 2 & pln Готово! & goto apartment2_1, Платье покороче
end

:apartment2_1_obisk
pln Я обыскала комнату, но не нашла денег.
goto apartment2_1

:apartment2_mirror
pln Я удивительно хороша собой, мне всегда говорили что я должна стать фотомоделью.
if dress = 0 then pln Облегающий костюм второго пилота хорошо подчеркивает мою фигуру.
if dress = 1 then pln Облегающее платье с глубоким вырезом хорошо подчеркивает фигуру и раскрывает мои лучшие качества.
if dress = 2 then pln Облегающее короткое платье хорошо подчеркивает фигуру и мои стройные загорелые ноги.
btn apartment2_1, Вернуться
end

/** MISSION 2 **/

:apartment1_2
:apartment1_3
:apartment1_4
:apartment1_5
:apartment1_6
pln Комната командора заперта.
goto ship

:earth
if money = 0 then pln Лучше сначала найти деньги, за место в космопорте взимается почасовая оплата. & goto mostik
mission = 2
fuel = 0
pln Я приземлилась на Землю и началась рутина. После того как с горючим и лишними пассажирами было покончено я вернулась на корабль.
pln Когда я зашла в рубку Вуд сидел в кресле пилота и перечитывал письмо "Коалы". Выглядел он плохо.
pln - Все готово к вылету командор! - отрапортовала я. - Припасы, вода, горючее - куплено, заправлено, проверено.
pln - Тогда вылетаем - задумчиво потянул он и снова уставился в письмо.
if count_apartment1_1_stolik_2 > 0 then btn earth1_1, - Командор, зачем вы искромсали ваши порножурналы?
btn earth2, Сесть в кресло второго пилота и запросить разрешение на взлет
end

:earth1_1
pln - Я вырезаю из них буквы, когда составляю письма где шантажирую... - Вуд подозрительно посмотрел на меня. - В общем
_ напоминай мне запирать каюту почаще.
pln Я торопливо закивала.
btn earth2, Сесть в кресло второго пилота и запросить разрешение на взлет
end

:earth2
pln После того как формальности были улажены, корабль начал медленно подниматься вверх. Через пару минут он
_ понесется на всей скорости к заданным мной координатам.
pln - Есть идеи, навигатор Андерсон? - простонал Вуд держась за голову. От смены гравитации ему стало еще хуже.
pln - Вам надо выпить таблетку... - я запнулась, поймав взгляд командора. - Ааа, вы про дело! Конечно. Они растворили себя в кислоте.
pln - Что??? - обомлел Вуд.
pln - Ну в кислоте. Это же все объясняет. Нет следов и тел. - терпеливо объяснила я.
pln - И зачем двадцати ученым с мировым именем растворять себя в кислоте - застонал командор.
pln Над этим вопросом я еще не думала, поэтому решила промолчать.
pln - Буду у себя в каюте - наконец прервал тишину Вуд и направился в сторону лестницы.
pln Полет начался, все датчики в норме. Будь я настоящим пилотом, я бы знала сколько еще осталось лететь. Но мне оставалось либо
_ ждать, либо погрузиться в крио-сон.
btn earth3, Впасть с крио-сон и ждать пока бортовой ИИ меня разбудит
btn earth3_flirt, Пойти соблазнять детектива Вуда
end

:earth3_flirt
pln Детектив был в комнате отдыха. Он лежал подушках со стаканом в руках и задумчиво изучал потолок.
pln Вуд молчал, поэтому диалог решила начать я:
btn flirt_e1, - Командор, почему вы так много пьете?
btn flirt_e2, - Командор, не нальете симпатичной девушке выпить?
end

:flirt_e1
pln - Я изобрел новый способ излечения наркомании. - похвастал Вуд, делая глоток. - Алкоголизмом.
btn flirt_e2, - Командор, не нальете симпатичной девушке выпить?
end

:flirt_e2
pln - Но ты сама их всех высадила. - удивился Вуд.
btn earth3, Вздохнуть и пойти впасть в крио-сон в своей каюте
end

/** MISSION 3 **/
:earth3
mission = 3
fuel = 3
pln Когда ИИ меня пробудил, я не узнала свою каюту - вокруг мигали красные лампочки и выла сирена. Что-то не так!
pln Внутри меня начала подниматься паника. Надо срочно что-то делать!
btn room_3, Искать командора!
end


:room_3
mission = 4
pln Я нашла детектива в комнате отдыха, он сидел в массажном кресле в наушниках с бутылкой виски в руке. Когда я зашла он
_ стащил с головы наушники и подозрительно огляделся по сторонам:
pln - Почему везде мигают красные лампочки, старший помощник Андерсон?
pln - В этом и дело, командор! - завопила я. - Корабль сломался.
pln - Нам повезло что на борту есть механик - успокоился Вуд натягивая на голову наушники. - Почини.
pln Я отрыла рот чтобы возразить, но не придумала что.
btn ship, Выйти из комнаты отдыха
end

/** MISSION 4 **/

:room_4
pln Я мне могу вернуться к командору не отремонтировав корабль.
goto ship

:engineroom_4
pln В машинное отделение занимает всю нижнюю палубу, тут сыро и куча непонятных устройств.
btn engineroom_4_2, Найти поломку
end

:engineroom_4_2
pln Тут выглядит сломанным абсолютно все.
btn engineroom_4_3, Починить что-нибудь
end

:engineroom_4_3
mission = 5
pln Я ударила тяжелым гаечным ключом по ближайшей электронной панели, но это не принесло видимых результатов.
btn ship, Подняться из машинного отделения
end

/** MISSION 5 **/

:engineroom_5
:engineroom_6
:engineroom_7
:engineroom_8
pln Больше я в машинное отделение ни ногой.
goto ship

:room_5
mission = 6
pln - Командор - воскликнула я врываясь в комнату. Вуд с интересом повернул голову на звук. На нем был шлем
_ виртуальной реальности. Я продолжила: - По штату я конечно пилот, механик и даже врач, но мы то знаем что
_ вы меня наняли за мою большую грудь и я ничего не умею.
pln Вуд молчал.
pln - Вы думаете что смотрите на меня, но на самом деле уставились на цветок в горшке, командор. - решила добавить я.
pln - Корабль автоматически передает сигнал бедствия, - не обратил внимания на мое замечание детектив, - мы с тобой застряли
_ тут вдвоем минимум на две недели.
pln - Вы намекаете на...
pln - Именно. - кивнул Вуд.
btn room_5_2, Пожать плечами и начать раздеваться
end

:room_5_2
image anderson_1.png
pln - У нас сохранилась связь и мы должны раскрыть это дело дистанционно, - продолжил Вуд объяснять растению. - Свяжись с
_ "Коалой" и запроси всю информацию какую возможно: данные с камер видеонаблюдения, схему корабля, цели миссии,
_ список членов экипажа. ВСЕ!
pln Закончив говорить командор отвернулся от цветка, потеряв к нему всякий интерес, и продолжил играть.
btn ship, Вздохнуть, одеться и выйти из комнаты отдыха
end

/** MISSION 6 **/

:room_6
pln Не хочу туда сейчас возвращаться.
goto ship

:connect6
mission = 7
pln На экране появилась галочка, подтверждающая что письмо успешно отправлено. Теперь остается только ждать.
goto mostik

/** MISSION 7 **/
:apartment1_7
pln Дверь была не заперта, поэтому я вошла. Командор был у себя и играл сам с собой в шахматы.
if count_apartment1_7 = 1 then pln - Отправила письмо - информировала я. - Остается только ждать.
if count_apartment1_7 = 1 then pln - Хорошо, квартмейстер Андерсон.

if count_quest_description > 0 and count_q_1 = 0  then btn q_1, - Командор, правда что у вас не было нераскрытых дел?
if count_flirt7 = 0 then btn flirt7, Соблазнить детектива Вуда
btn ship, Выйти из каюты
end

:q_1
pln - Конечно были. - Задумчивое лицо Вуда неожиданно озарилось усмешкой, он быстро схватил продолговатую белую фигуру с
_ доски и переставил ее в другое место.
pln - Шах! - радостно закричал детектив, затем пересел в кресло по другую сторону стола, погрустнел и снова задумался.
pln Похоже командор уже забыл про мой вопрос.
if count_flirt7 = 0 then btn flirt7, Соблазнить детектива Вуда
btn ship, Выйти из каюты
end

:flirt7
pln - Не хотите как-нибудь скоротать время, командор? - невинно предложила я.
pln - Ты все равно не умеешь играть в шахматы - отмахнулся Вуд.
if count_quest_description > 0 and count_q_1 = 0  then btn q_1, - Командор, правда что у вас не было нераскрытых дел?
btn ship, Выйти из каюты
end

:apartment2_7
pln Я зашла в свою каюту и закрыла за собой дверь.
btn apartment2_7_sleep, Поспать
btn ship, Выйти из каюты
end

:apartment2_7_sleep
mission = 8
pln Усталось дает о себе знать и я быстро засыпаю.
btn ship, Проснуться и выйти из каюты
end

:room_7
pln В комнате отдыха никого нет.
btn room_7_barmen, Воспользоваться роботом-барменом
btn ship, Выйти из комнаты
end

:room_7_barmen
mission = 8
pln Я наливаю себе виски и сажусь в кресло. Сама того не замечая я засыпаю.
btn ship, Проснуться и выйти из комнаты
end

/** MISSION 8 **/

:read8
mission = 9
pln Компьютер выдал длинную распечатку. Пробежавшись по ней глазами я поняла что надо срочно идти к командору.
btn ship, Спуститься с мостика
end

:apartment1_8
pln Надо сначала проверить, есть ли новости от "Коалы", затем докладывать командору.
goto ship

/** MISSION 9 **/
:apartment1_9
mission = 10
pln - Командор! "Коала" выслали все что им известно. - вбежав в каюту выкрикнула я.
pln Вуд недоуменно посмотрел на меня:
pln - Что за паника? Я чего-то не знаю?
pln - У нас всего семь часов. Они уничтожат корабль под прессом, он настолько радиоактивен что его опасно держать возле
_ станции. Если нам не хватает данных, которые мы имеем, "Коала" успеет отправить внутрь еще одну экспедицию, но
_ совсем ненадолго. Внутри, даже в самой тяжелой защите от гамма-лучей, людям опасно находиться более двадцати минут,
_ а броня потом начинает светиться как лампочка! Нам нужно конкретно сообщить им что искать. - быстро затараторила я.
pln Вуд взял у меня из рук отчет и пробежался по нему, повторив в слух:
pln - Мозги всей техники внутри поджарились. У робота, которого послала "Коала" первым, тоже сгорела начинка. Записи с
_ камер искать бессмысленно. Отряд, который заходил на корабль, лишь быстро пробежался по комнатам в поисках трупов и тут же вышли.
_ Ни одного тела найдено не было.
pln - Это наверное самое сложное ваше дело, командор? - поинтересовалась я.
pln Вуд фыркнул и не ответив мне продолжил:
pln - Самый высокий уровень радиации в лаборатории, где проводились исследования. Вероятно, такое необычное гамма-излучение
_ - это результат одного из их опытов.
pln - Посмотрите список членов экипажа, командор! Это же были лучшие умы галактики!
pln Вуд снова фыркнул, но на этот раз презрительно:
pln - Я пойду на мостки и отвечу им чтобы собирали еще одну экспедицию на корабль. Их корабль очень похож на увеличенную
_ копию нашего. Пройдись на нашему, подумай какие вопросы мы еще можем им задать. У нас один шанс.
btn ship, Кивнуть и выйти из каюты
btn flirt9, Соблазнить детектива Вуда
end

:flirt9
pln - Командор, как насчет секса? - прямо спросила я.
pln - От секса люди не пропадают - терпеливо объяснил Вуд. - Думай дальше.
btn ship, Выйти из каюты
end

:apartment2_9
:shower_9
:engineroom_9
:room_9
:gateways_9
:kitchen_9
pln На это нет времени!
goto ship

/** MISSION 10 **/
:apartment1_10
pln Каюта командора закрыта, но она почти ничем не отличается от моей.
goto ship

:apartment2_10
pln Я в своей каюте.
if var_7 = 0 then btn proc var7, Подумать про каюты экипажа
btn ship, Выйти из каюты
end

:kitchen_10
pln Это кухня. У нас на корабле нет лаборатории, поэтому представлю что это еще и лаборатория. И мед. отсек.
if var_5 = 0 then btn proc var5, Подумать про медикаменты
if var_3 = 0 then btn proc var3, Подумать про записи исследований
if tube = 0 then btn tube = 1 & cls & goto kitchen_take2, Взять <p style="color: red">красный</p> тюбик
if tube = 0 then btn tube = 2 & cls & goto kitchen_take2, Взять <p style="color: green">зеленый</p> тюбик
if tube = 0 then btn tube = 3 & cls & goto kitchen_take2, Взять <p style="color: blue">синий</p> тюбик
btn ship, Вернуться в коридор
end

:kitchen_take2
pln Я беру тюбик из холодильника.
if var_5 = 0 then btn proc var5, Подумать про медикаменты
if var_3 = 0 then btn proc var3, Подумать про записи исследований
btn ship, Вернуться в коридор
end

:shower_10
pln Душевая. На какие мысли может навести меня эта комната?
if var_4 = 0 then btn proc var4, Подумать про следы борьбы
btn ship, Вернуться в коридор
end

:engineroom_10
pln В машинном отделении всякие железки и коробки.
if var_8 = 0 then btn proc var8, Подумать о поломках корабля
btn ship, Вернуться в коридор
end

:room_10
pln В комнате отдыха пусто. Интересно, была ли на корабле-призраке комната отдыха?
btn ship, Вернуться в коридор
end

:gateways_10
pln Я в шлюзовом отделении. Это единственный выход из корабля. Здесь же расположены скафандры.
if var_6 = 0 then btn proc var6, Подумать про скафандры
if var_2 = 0 then btn proc var2, Подумать про обшивку
btn ship, Вернуться в коридор
end

:var1
pln Искать следы кислоты - по-моему отличная идея. Куда-то ведь люди делись? Запишу на всякий случай.
vars = vars + 1
var_1 = 1
end
:var2
pln "Коала" нам уже сообщила что обшивка не повреждена, но может быть они что-то упустили. Например следы взлома. Возьму версию на заметку.
vars = vars + 1
var_2 = 1
end
:var3
pln Записи исследований - это может быть важно. Пропажа экипажа могла быть результатом опыта. Например с кислотой. Записала.
vars = vars + 1
var_3 = 1
end
:var4
pln Следы борьбы могут быть ключом к разгадке. Экипаж могли похитить, или произошел конфликт между членами экипажа.
vars = vars + 1
var_4 = 1
end

:var5
pln Если какие-то из медикаментов пропали, это может сказать нам о многом. Может экипаж был чем-то болен? Запишу эту версию.
vars = vars + 1
var_5 = 1
end
:var6
pln Пропавшие скафандры? Не знаю чем это поможет, но записать стоит.
vars = vars + 1
var_6 = 1
end

:var7
pln Обыскать каюты экипажа в поисках чего-нибудь необычного. Не уверена, кают много. Но как версию можно пока отложить.
vars = vars + 1
var_7 = 1
end
:var8
pln Если некоторые поломки корабля были вызваны не радиацией, то могут быть причиной случившегося. Как вариант - годится.
vars = vars + 1
var_8 = 1
end

/*
 * Корабль
 */
:ship
if mission < 10 then pln Я в длинном коридоре, которым разделены помещения корабля. Куда направиться?
if mission = 10 then pln Я в длинном коридоре, которым разделены помещения корабля. Мне надо внимательно обойти корабль
_ и решить, какой вопрос мы зададим "Коале", что угодно может навести меня на правильную мысль.
btn mostik, Мостик
btn kitchen_#mission$, Кухня
if mission < 9 then btn shower, Душевая
if mission > 9 then btn shower_#mission$, Душевая
btn apartment1_#mission$, Каюта командора
btn apartment2_#mission$, Каюта второго пилота
btn engineroom_#mission$, Машинное отделение
btn room_#mission$, Комната отдыха
btn gateways_#mission$, Шлюзы
if mission = 10 and var_1 = 0 then btn proc var1, Подумать о следах кислоты
end

:mostik
p Я на хожусь на мостике корабля
if mission = 1 then pln , мы вертимся на орбите недалеко от Земли. Надо приземлиться и запастись припасами.
if mission = 2 then pln , мы стоим на территории Космопорт. Надо заправить корабль и купить припасов.
if mission >= 3 then pln , вся рубка сигналит о неисправностях, приборы сошли с ума.
if mission = 8 then pln У вас 1 непрочитанное сообщение.
if mission = 10 then pln В кресле пилота сидит Вуд.
if mission = 10 then pln - Есть варианты, квартмейстер Андерсон?
if mission < 10 then btn check_pribori, Проверить приборы
if mission < 10 then btn quest_description, Прочитать описание задания
if mission = 1 then btn earth, Приземлиться
if mission = 6 then btn connect6, Сделать запрос "Коале"
if mission = 8 then btn read8, Прочитать ответ "Коалы"
if mission = 10 and vars > 2 then btn end_select, - Так точно командор, ответы готовы
if mission = 10 then btn ship, - Еще нет, пойду поищу дальше else btn ship, Спуститься с мостика
if mission = 10 and tube > 0 then btn mostik2, - Еще нет, но я принесла вам перекусить. Ваш любимый тюбик с едой
end

:mostik2
p - Терпеть не могу
if tube = 1 then p  <span style="color: red">красный</span> тюбик
if tube = 2 then p  <span style="color: green">зеленый</span> тюбик
if tube = 3 then p  <span style="color: blue">синий</span> тюбик
pln , - возмутился Вуд. - Где ты его взяла? Я думал что все их выбросил.
tube = 0
btn ship, Спуститься с мостика
end

:shower
pln Комната с душевой кабинкой, единственной на корабль.
btn shower2, Принять душ, запереть за собой дверь
if count_shower4 = 0 and mission > 1 then btn shower4, Принять душ, оставить дверь приоткрытой else btn shower3, Принять душ, оставить дверь приоткрытой
btn ship, Выйти из душевой
end

:shower2
if mission = 7 then mission = 8
pln Я принимаю бодрящий душ. Очень освежает!
btn ship, Выйти из душевой
end
:shower3
if mission = 7 then mission = 8
pln Я принимаю бодрящий душ, поглядывая, не пройдет ли мимо по коридору детектив. Но к сожалению все тихо.
btn ship, Выйти из душевой
end
:shower4
if mission = 7 then mission = 8
pln Я принимаю бодрящий душ, поглядывая, не пройдет ли мимо по коридору детектив. В коридоре послышались шаги.
btn shower5, Мечтать
end
:shower5
pln - Сейчас командор ворвется в кабинку и грубо возьмет меня. - я прикусила губу в предвкушении.
pln Шаги отдаляются. Командор прошел мимо.
btn ship, Вздохнуть и выйти из душевой
end

:quest_description
pln Покопав папке со спамом я нашла нужное мне письмо:
pln "Детектив Вуд, вас приветствует исследовательский центр "Коала". Наш корабль, экипажем которого двигали исключительно
_ исследовательскими цели, совершенно случайно пролетал через заброшенный 18ый сектор, где с ним прервалась всякая
_ связь. Нашими силами корабль был отбуксирован к одной из станций "Коалы". У корабля нет повреждений обшивки, но вся
_ электроника вышла из строя - на борту просто зашкаливающий уровень радиации! И самое главное: на борту мы не
_ нашли следов ни одного из двадцати членов экипажа! Остановок по пути не было.
pln Детектив Вуд, нам известно что вы лучший и у вас не было ни одного дела с которым вы бы не
_ справились. Пожалуйста, помогите нам решить эту головоломку. Если вы согласны то мы вышлем вам аванс и координаты немедленно."
pln Какое странно дело! Наверняка у командора уже есть ответ на эту головоломку, поэтому он и не хотел за нее браться.
btn mostik, Назад
end

:check_pribori
pln Я бегло пробежалась глазами по датчикам:
if mission >= 3 then p Все системы отказали. else p Все системы функционируют.
if fuel = 0 then p  Нет горючего.
if fuel = 1 then p  Горючее на исходе.
if fuel = 2 then p  Горючее в норме.
if fuel = 3 then p  Уровень горючего неизвестен.
if mission >= 3 then p Уровень загрязнения неизвестен. else p  Уровень загрязнения - 3. Рекомендуется выпустить робота-уборщика.
btn mostik, Назад
end



:end_select
pln Вуд повернулся ко мне:
pln - Я их убедил проверить несколько вещей. Слушаю тебя внимательно.

if var_1 = 1 then btn cls & selected_1 = 1 & goto end_selected2, Искать следы кислоты
if var_2 = 1 then btn cls & selected_1 = 2 & goto end_selected2, Искать следы взлома извне
if var_3 = 1 then btn cls & selected_1 = 3 & goto end_selected2, Искать записи исследований
if var_4 = 1 then btn cls & selected_1 = 4 & goto end_selected2, Искать следы борьбы
if var_5 = 1 then btn cls & selected_1 = 5 & goto end_selected2, Проверить оставшиеся медикаменты
if var_6 = 1 then btn cls & selected_1 = 6 & goto end_selected2, Проверить количество скафандров
if var_7 = 1 then btn cls & selected_1 = 7 & goto end_selected2, Обыскать каюты экипажа
if var_8 = 1 then btn cls & selected_1 = 8 & goto end_selected2, Проверить системы корабля
end

:end_selected2
pln - И что будем проверять во вторую очередь?

if var_1 = 1 and selected_1 != 1 then btn cls & selected_2 = 1 & goto end_selected3, Искать следы кислоты
if var_2 = 1 and selected_1 != 2 then btn cls & selected_2 = 2 & goto end_selected3, Искать следы взлома извне
if var_3 = 1 and selected_1 != 3 then btn cls & selected_2 = 3 & goto end_selected3, Искать записи исследований
if var_4 = 1 and selected_1 != 4 then btn cls & selected_2 = 4 & goto end_selected3, Искать следы борьбы
if var_5 = 1 and selected_1 != 5 then btn cls & selected_2 = 5 & goto end_selected3, Проверить оставшиеся медикаменты
if var_6 = 1 and selected_1 != 6 then btn cls & selected_2 = 6 & goto end_selected3, Проверить количество скафандров
if var_7 = 1 and selected_1 != 7 then btn cls & selected_2 = 7 & goto end_selected3, Обыскать каюты экипажа
if var_8 = 1 and selected_1 != 8 then btn cls & selected_2 = 8 & goto end_selected3, Проверить системы корабля
end

:end_selected3
pln - И в третью?

if var_1 = 1 and selected_1 != 1 and selected_2 != 1 then btn cls & selected_3 = 1 & goto end_selected4, Искать следы кислоты
if var_2 = 1 and selected_1 != 2 and selected_2 != 2 then btn cls & selected_3 = 2 & goto end_selected4, Искать следы взлома извне
if var_3 = 1 and selected_1 != 3 and selected_2 != 3 then btn cls & selected_3 = 3 & goto end_selected4, Искать записи исследований
if var_4 = 1 and selected_1 != 4 and selected_2 != 4 then btn cls & selected_3 = 4 & goto end_selected4, Искать следы борьбы
if var_5 = 1 and selected_1 != 5 and selected_2 != 5 then btn cls & selected_3 = 5 & goto end_selected4, Проверить оставшиеся медикаменты
if var_6 = 1 and selected_1 != 6 and selected_2 != 6 then btn cls & selected_3 = 6 & goto end_selected4, Проверить количество скафандров
if var_7 = 1 and selected_1 != 7 and selected_2 != 7 then btn cls & selected_3 = 7 & goto end_selected4, Обыскать каюты экипажа
if var_8 = 1 and selected_1 != 8 and selected_2 != 8 then btn cls & selected_3 = 8 & goto end_selected4, Проверить системы корабля
end

:end_selected4
pln Мы отправили запрос "Коале". Оставалось только ждать.
btn end_selected5, Ждать
end

:end_selected5
if selected_1 = 5 and selected_2 = 6 then goto end3
if selected_1 = 6 and selected_2 = 5 then goto end3

if selected_2 = 5 and selected_3 = 6 then goto end3
if selected_3 = 6 and selected_2 = 5 then goto end3

if selected_1 = 5 and selected_3 = 6 then goto end3
if selected_3 = 6 and selected_1 = 5 then goto end3

if selected_1 = 5 then goto end2
if selected_1 = 6 then goto end2

if selected_2 = 5 then goto end2
if selected_2 = 6 then goto end2

if selected_3 = 5 then goto end2
if selected_3 = 6 then goto end2
goto end1
end

:end1
pln Прочитав ответ "Коалы" Вуд только покачал головой:
pln - Экспедиция нам ничего не дала.
pln Я предложила еще несколько теорий с разными видами кислоты, но Вуд все их отмел. Мы так и не смогли найти ответ на
_ эту головоломку. Через какое-то время мы получили сообщение от "Коалы" что корабль был отправлен под пресс.
pln До нашего спасения оставалась еще примерно неделя, и все это время командор пил. Не знаю, спал ли он.
pln Дни тянулись бесконечно, мы почти не разговаривали, каждый думал о своем.
pln Когда за нами наконец прилетели и отбуксировали наш корабль на ближайшую станцию, мы сели в кафе космопорта и смогли поговорить:
pln - Командор - начала я. - Нам просто не повезло. Если бы не поломка...
pln - Знаю - кивнул Вуд. - Но чувствую что мог бы разгадать эту тайну. Мы что-то упустили.
pln Больше мы об этом деле не говорили. Через пару недель "Коала" прислала нам оставшуюся часть денег, мы отремонтировали
_ корабль и снова отправились странствовать.
pln Я еще долго вспоминала этот случай как один из самых загадочных за карьеру детектива Вуда. Но правду нам узнать так и не довелось.
end

:end2
pln Прочитав ответ "Коалы" Вуд задумчиво начал расхаживать по рубке:
pln - Чего-то не хватает.
pln Я предложила еще несколько теорий с разными видами кислоты, но Вуд все их отмел. Мы так и не успели найти ответ на
_ эту головоломку. Через какое-то время мы получили сообщение от "Коалы" что корабль был отправлен под пресс.
pln До нашего спасения оставалась еще примерно неделя, и все это время командор пил. Не знаю, спал ли он.
pln Дни тянулись бесконечно, мы почти не разговаривали, каждый думал о своем.
pln Когда за нами наконец прилетели и отбуксировали наш корабль на ближайшую станцию, мы сели в кафе космопорта и смогли поговорить:
pln - Командор - начала я. - Нам просто не повезло. Если бы не поломка...
pln - Знаю - кивнул Вуд. - Но уже ничего не поделаешь. К сожалению я нашел ответ слишком поздно.
pln - Вы нашли ответ? - удивилась я. - Расскажите, командор!
pln - Нет. - отрезал Вуд. - Тогда груз вины ляжет и на тебя. Я унесу тайну этого дела с собой в могилу.
pln С тех пор я пыталась еще несколько раз разговорить Вуда, но он был непреклонен. Через пару недель "Коала" прислала нам
_ оставшуюся часть денег, мы отремонтировали корабль и снова отправились странствовать.
pln Я еще долго вспоминала этот случай как один из самых загадочных за карьеру детектива Вуда. Но правду мне узнать так и не довелось.
end

:end3
pln Прочитав ответ "Коалы" Вуд просто выпрыгнул из кресла:
pln - Ну конечно!
pln И поймав мой любопытный взгляд сказал уже обращаясь ко мне:
pln - Быстро пиши "Коале" чтобы не уничтожали корабль, а осторожно разобрали. Сняли корпус.
pln - Но командор.. - потерялась я.
pln - Прямо сейчас! Объяснения потом! - отрезал Вуд. - Вопрос жизни и смерти!
pln После того как письмо было отправлено, Вуд расслабился, сходил за виски, и, вернувшись, устроился на соседнем кресле:
pln - Ученые были слишком далеко от цивилизации. Без связи и специального оборудования. - принялся объяснять Вуд. -
_ После неудачного опыта, повлекшего за собой сильное радиационное заражение, им оставалось только лечь и умирать.
pln - Но они придумали что-то еще? - догадалась я.
pln - Именно. - похвалил Вуд. - Их бы не защитили ни скафандры, ни тонкие внутренние перегородки корабля. Им <i>пришлось</i>
_ придумать что-то еще.
pln Отхлебнув из стакана, Вуд продолжил:
pln - Знаешь то единственное что на подобных кораблях рассчитано на воздействие гамма-лучей и защиту от них?
pln Я открыла рот чтобы ответить "Нет, командор", но Вуд уже продолжил:
pln - Конечно знаешь, ты же пилот, Андерсон! Корпус! Корпус это толстенный многослойный пласт металла защищающий нас от
_ агрессивной среды.
pln Я понимающе закивала, хотя не понимала к чему ведет командор.
pln - Приняв все что было на борту из лекарств против радиации, они надели скафандры и вышли в открытый космос. Нашли
_ место в корпусе куда сложнее всего добраться радиации и где могут поместиться все и, думаю с помощью перепрограммированного
_ робота-ремонтника, разобрали обшивку, влезли туда. Робот установил панели обшивки назад.
pln - Какой ужас, командор! - испугалась я. - Значит они уже давно мертвы?
pln - Скорее всего нет. - пожал плечами Вуд. - На любом корабле рассчитанном на долгие рейсы есть препараты замедляющие
_ метаболизм и способные ввести в кому. Когда полеты длятся по двадцать лет - это весьма полезно.
_ На борту было два врача, я думаю они догадались их применить. Они спят.
pln - Что-то не сходится, командор. - задумалась я. - Первым делом они бы думали о том как выбраться. Неужели они не оставили записку?
pln - Конечно оставили. - согласился Вуд. - И думаю что не одну.
_ Как их пропустили? Спешка, помещение без освещения, может быть искали тепловизорами. Добавь тяжелую броню и узкий обзор.
_ Ладно, согласен что это слабые версии, там должны были быть все стены и корпус исписаны посланиями. Узнаем все в отчете.
pln Я задумалась, но вопросов больше в голову не пришло. Видимо правду мы узнаем только прочитав ответ от "Коалы".
btn end3_1, Спустя несколько недель
end

:end3_1
pln "Коала" почти сразу выслала нам благодарность и оставшуюся сумму, с обещаем выслать подробный отчет, но на деле
_ отчет от них мы получили только через несколько недель. Тогда мы сидели в кафе космопорта и пили кофе.
pln - Командор, вы ошиблись. - сообщила я читая долгожданное письмо.
pln - Жаль. - пожал плечами Вуд. - Красивая была теория. Тогда твоя версия с кислотой мне уже кажется не такой уж и...
pln - Нет-нет, они действительно были под корпусом - начала я рассказывать суть отчета. - Включив сигнал бедствия они
_ решили переждать там, пока их кто-нибудь не спасет. Все впали к крио-сон, кроме одного человека. Он следил за
_ уровнем кислорода, менял баллоны и периодически возвращался на корабль чтобы проверить, не поймал ли кто их сигнал бедствия.
pln - И должен был всех разбудить. Ну конечно, вот почему не было записки! - воскликнул Вуд. - И что с ним случилось?
pln - В одну из ночей повреждение обшивки обнаружил робот-ремонтник и устранил ее. Они остались погребены внутри.
pln - Как глупо. - поморщился Вуд. - Неудивительно что мне не пришло это в голову!
pln - Судя по всему они думали что все роботы вышли из строя. Но у ремонтников хорошая защита от радиации, так как
_ им часто приходится работать снаружи. Автономность тоже на высоте. - помрачнела я. - Хорошо что у нас не работает ни
_ один робот кроме бармена, потому что вы выбросили их внутренности чтобы провозить в них наркотики.
pln - Так что стало с тем беднягой который не спал? - сменил тему Вуд.
pln - Он умер, командор. В окружении воды, которую он туда натаскал чтобы защитить от облучения, он умер от жажды.
pln Командор кивнул.
pln - Все остальные выжили?
pln - Они очень слабы и обезвожены - крио-сон происходил при слишком низкой температуре. Но уже стабильны. Их коллега
_ перед смертью поставил самые емкие баллоны с кислородом.
pln Командор снова кивнул. Допили кофе мы в молчании.
end









